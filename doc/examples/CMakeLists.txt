FILE(
	GLOB_RECURSE
	TBTK_DOCUMENTATION_EXAMPLE_SRC
	RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}
	*.cpp
)

IF(NOT ${COMPILE_ARNOLDI_ITERATOR})
	LIST(
		REMOVE_ITEM
		TBTK_DOCUMENTATION_EXAMPLE_SRC
		Solver/ArnoldiIterator.cpp
		PropertyExtractor/ArnoldiIterator.cpp
	)
ENDIF(NOT ${COMPILE_ARNOLDI_ITERATOR})

SET(TBTK_QUIET TRUE)
FIND_PACKAGE(TBTK QUIET)

INCLUDE_DIRECTORIES(include)

FOREACH(FILE ${TBTK_DOCUMENTATION_EXAMPLE_SRC})
	CONFIGURE_FILE(
		${CMAKE_CURRENT_SOURCE_DIR}/${FILE}
		${CMAKE_CURRENT_BINARY_DIR}/${FILE}
	)

	GET_FILENAME_COMPONENT(NAME ${FILE} NAME_WE)
	GET_FILENAME_COMPONENT(PATH ${FILE} DIRECTORY)
	SET(APPLICATION_NAME Example${PATH}${NAME})

	SET(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/build/${PATH}/)
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11 -Wall -O3")
	ADD_EXECUTABLE(${APPLICATION_NAME} ${FILE})
	SET_TARGET_PROPERTIES(${APPLICATION_NAME} PROPERTIES OUTPUT_NAME ${NAME})
	SET_TARGET_PROPERTIES(${APPLICATION_NAME} PROPERTIES EXCLUDE_FROM_ALL TRUE)
	TARGET_LINK_LIBRARIES(${APPLICATION_NAME} ${TBTK_LIBRARIES})

	FILE(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/output/${PATH}/)
	SET(TBTK_DOCUMENTATION_EXAMPLE_OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/output/${PATH}/${NAME}.output)
	ADD_CUSTOM_COMMAND(
		OUTPUT ${TBTK_DOCUMENTATION_EXAMPLE_OUTPUT}
		COMMAND ${CMAKE_CURRENT_BINARY_DIR}/build/${PATH}/${NAME} > ${TBTK_DOCUMENTATION_EXAMPLE_OUTPUT}
		DEPENDS ${APPLICATION_NAME}
	)
	STRING(REPLACE "/" "_" TBTK_DOCUMENTATION_EXAMPLE_TARGET ${TBTK_DOCUMENTATION_EXAMPLE_OUTPUT})
	ADD_CUSTOM_TARGET(
		${TBTK_DOCUMENTATION_EXAMPLE_TARGET}
		DEPENDS ${TBTK_DOCUMENTATION_EXAMPLE_OUTPUT}
	)
	LIST(APPEND TBTK_DOCUMENTATION_EXAMPLE_OUTPUTS ${TBTK_DOCUMENTATION_EXAMPLE_TARGET})
ENDFOREACH(FILE ${TBTK_DOCUMENTATION_EXAMPLE_SRC})
SET(TBTK_DOCUMENTATION_EXAMPLE_OUTPUTS ${TBTK_DOCUMENTATION_EXAMPLE_OUTPUTS} PARENT_SCOPE)
